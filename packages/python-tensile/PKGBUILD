# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
pkgname=python-tensile-git
provides=('python-tensile')
conflicts=('python-tensile')
pkgver=0
pkgrel=1
pkgdesc="benchmark-driven backend libraries for general matrix-matrix multiplications"
arch=('any')
url='https://rocm.docs.amd.com/projects/Tensile/en/latest/'
license=('MIT')
depends=('python' 'python-msgpack' 'python-pyaml' 'python-joblib')
makedepends=('cmake' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
makedepends+=('git')
source=("git+https://github.com/ROCm/rocm-libraries.git#branch=develop")
sha256sums=('SKIP')
_root="rocm-libraries"
_dir="$_root/shared/tensile"

pkgver() {
	cd $_root
	set -o pipefail
 	git describe --tags --long 2>/dev/null \
    	| sed 's/^v//; s/-\([0-9][0-9]*\)-g/.r\1.g/; s/-/./g' \
    	|| printf '0.r%s.g%s\n' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  	set +o pipefail
}

build() {
	cd "$_dir"
	python -m build --wheel --no-isolation
}

package() {
	cd "$_dir"
	python -m installer --destdir="$pkgdir" dist/*.whl
	# cmake files are installed to the wrong location, /usr/cmake.
	# Move them to the right directory.
	mkdir -p "$pkgdir"/usr/lib/cmake/Tensile
	mv "$pkgdir"/usr/cmake/* "$pkgdir"/usr/lib/cmake/Tensile
	rm -rf "$pkgdir"/usr/cmake
	install -vDm644 LICENSE.md "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
