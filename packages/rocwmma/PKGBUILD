# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>

pkgname=rocwmma-git
provides=('rocwmma')
conflicts=('rocwmma')
pkgver=6.4.3
pkgrel=2
pkgdesc='Library for accelerating mixed precision matrix multiplication'
arch=('x86_64')
url='https://rocm.docs.amd.com/projects/rocWMMA/en/latest/'
license=('MIT')
depends=('rocm-core' 'hip-runtime-amd' 'rocblas' 'openmp')
makedepends=('rocm-cmake' 'doxygen' 'rocm-toolchain')
source=("git+https://github.com/ROCmSoftwarePlatform/rocWMMA#branch=develop")
sha256sums=('SKIP')
_dirname="rocWMMA"
options=(!lto)

pkgver() {
  cd $_dirname
  set -o pipefail
  git describe --tags --long 2>/dev/null \
    sed 's/^v//; s/-\([0-9][0-9]*\)-g/.r\1.g/; s/-/./g' \
    || printf '0.r%s.g%s\n' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  set +o pipefail
}

prepare() {
    sed -i 's/if(F8_EXISTS)/if(1)/g' rocWMMA/CMakeLists.txt
}

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dirname"
        -D CMAKE_POLICY_VERSION_MINIMUM=3.5
        -D CMAKE_BUILD_TYPE=Release
        -D CMAKE_CXX_COMPILER=amdclang++
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
        -D ROCWMMA_BUILD_TESTS=OFF
        -D ROCWMMA_BUILD_SAMPLES=OFF
        -D ROCM_SYMLINK_LIBS=OFF
        -D HIP_PLATFORM=amd
        -D GPU_TARGETS="gfx1100;gfx1151"
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm644 "$_dirname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
